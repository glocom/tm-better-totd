namespace TMX {
    const string getMapByUidEndpoint = "https://trackmania.exchange/api/maps?fields=Tags,Name,MapId,MapUid&uid={uid}";
    const int maxTmxUidsLength = 60; 
    // <https://api2.mania.exchange/Method/Index/53>
    Json::Value@ GetMapFromUid(const string &in uid) {
        string url = getMapByUidEndpoint.Replace("{uid}", uid);
        auto req = PluginGetRequest(url);
        req.Start();
        while (!req.Finished()) yield();
        if (req.ResponseCode() >= 400 || req.ResponseCode() < 200 || req.Error().Length > 0) {
            log_warn("[status:" + req.ResponseCode() + "] Error getting map by UID from TMX: " + req.Error());
            return null;
        }
        // log_info("Debug tmx get map by uid: " + req.String());
        return Json::Parse(req.String());
    }

    // <https://api2.mania.exchange/Method/Index/53>
    Json::Value@ GetMapsByUids(string[] &in uids) {        
        if (uids.Length > maxTmxUidsLength) throw("Too many uids passed in at once");
        string url = getMapByUidEndpoint.Replace("{uid}", Text::Join(uids, ","));
        auto req = PluginGetRequest(url);
        req.Start();
        while (!req.Finished()) yield();
        if (req.ResponseCode() >= 400 || req.Error().Length > 0) {
            log_warn("[status:" + req.ResponseCode() + "] Error getting map by UIDs from TMX. URL: " + url + "; Error: " + req.Error());
            return null;
        }
        return Json::Parse(req.String());
    }

    void OpenTmxTrack(int TrackID) {
#if DEPENDENCY_MANIAEXCHANGE
        try {
            if (Meta::GetPluginFromID("ManiaExchange").Enabled) {
                ManiaExchange::ShowMapInfo(TrackID);
                return;
            }
        } catch {}
#endif
        OpenBrowserURL("https://trackmania.exchange/s/tr/" + TrackID);
    }

    void OpenTmxAuthor(int TMXAuthorID) {
#if DEPENDENCY_MANIAEXCHANGE
        try {
            if (Meta::GetPluginFromID("ManiaExchange").Enabled) {
                ManiaExchange::ShowUserInfo(TMXAuthorID);
                return;
            }
        } catch {}
#endif
        OpenBrowserURL("https://trackmania.exchange/user/profile/" + TMXAuthorID);
    }
}
